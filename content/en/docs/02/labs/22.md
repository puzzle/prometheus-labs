---
title: "2.2 Tasks: PromQL"
weight: 2
sectionnumber: 2
---

### Task 1

* Show the total of all samples scraped by Prometheus
* Use [Prometheus Aggregation operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#aggregation-operators) to sum the returned metrics

### Task 2

* Display the current CPU idle of the Prometheus server in %

{{% alert title="Hint" color="primary" %}}
Read the [documentation](https://prometheus.io/docs/prometheus/latest/querying/functions/) about the `rate()` function.
{{% /alert %}}

### Task 3

* Display the current CPU usage of the Prometheus server in %
* You can reuse the expression from the task above with a basic [Arithmetic binary operator](https://prometheus.io/docs/prometheus/latest/querying/operators/#arithmetic-binary-operators)

### Task 4

* Check how much free memory is available on the Prometheus server in %


### Task 5

* Check if the Prometheus server has more than 20% memory available using a [Comparison binary operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#comparison-binary-operators)

### Task 6

* Display the numbers of requests to `/metrics` on your Prometheus server
* Use the `0.1` bucket to just show the total numbers of requests below 0.1 seconds since starting the Prometheus server
* Analyze the query in [PromLens](https://demo.promlens.com/)

{{% alert title="Hint" color="primary" %}}
Read the [documentation](https://prometheus.io/docs/concepts/metric_types/#histogram) about the `histogram` metric type.
{{% /alert %}}

### Task 7

* Calculate the request duration of the 0.9 quantile on Prometheus server
* Use the [histogram_quantile](https://prometheus.io/docs/prometheus/latest/querying/functions/#histogram_quantile) function
* Filter the query to show only the `/metrics` handler

### Task 8 (advanced)

* Find a metric that shows you the available disk space on filesystem mounted on `/`
* Use a function that allows you to predict when the filesystem will be full in 4 hours
* Predict the usage linearly based on the growth over the last 1 hour

### Task 9 (advanced)

* Show the uptime of your VM in minutes
* Add a label containing the kernel release

## Solutions

{{% details title="Task 1" %}}

Expression
```promql
sum(scrape_samples_scraped)
```

{{% /details %}}

{{% details title="Task 2" %}}

Expression
```promql
rate(
  node_cpu_seconds_total{mode="idle"}[5m]
  )
* 100
```

{{% /details %}}

{{% details title="Task 3" %}}

Substract idle CPU usage from 1 (100%)

```promql
(
  1 -
  rate(
      node_cpu_seconds_total{mode="idle"}[5m]
      )
)
* 100
```

{{% /details %}}

{{% details title="Task 4" %}}

```promql
sum by(instance) (node_memory_MemAvailable_bytes)
/
sum by(instance) (node_memory_MemTotal_bytes)
* 100
```
{{% /details %}}


{{% details title="Task 5" %}}

```promql
sum by(instance) (node_memory_MemAvailable_bytes)
/
sum by(instance) (node_memory_MemTotal_bytes)
* 100
> 20
```

{{% /details %}}

{{% details title="Task 6" %}}

Expression
```promql
prometheus_http_request_duration_seconds_bucket{handler="/metrics",le="0.1"}
```

You can calculate how many requests in % were below 0.1 seconds by aggregating above metric. See more information about Apdex score at [Prometheus documentation](https://prometheus.io/docs/practices/histograms/#apdex-score)

Example

```promql
sum(
  rate(
    prometheus_http_request_duration_seconds_bucket{handler="/metrics",le="0.1"}[5m]
  )
) by (job, handler)
/
sum(
  rate(
    prometheus_http_request_duration_seconds_count{handler="/metrics"}[5m]
  )
) by (job, handler)
* 100
```
{{% /details %}}

{{% details title="Task 7" %}}

Expression
```promql
histogram_quantile(
  0.9,
  rate(
    prometheus_http_request_duration_seconds_bucket{handler="/metrics"}[5m]
  )
)
```
Explanation: `histogram_quantile` will calculate the 0.9 quantile based on the samples distribution in our buckets by assuming a linear distribution within a bucket.

{{% /details %}}

{{% details title="Task 8" %}}

Expression
```promql
predict_linear(node_filesystem_avail_bytes{mountpoint="/"}[1h], 3600 * 4) < 0
```

Explanation: based on data over the last `1h`, the disk will be `< 0` bytes in `3600 * 4`s.
The query will return `no data` because the file system will not be full in the next 4 hours. You can check how much disk space will be available in 4 hours by removing the `< 0` part.
```promql
predict_linear(node_filesystem_avail_bytes{mountpoint="/"}[1h], 3600 * 4)
```

{{% /details %}}

{{% details title="Task 9" %}}

Expression
```promql
(
  (
    time() - node_boot_time_seconds
  ) / 60
)
* on(instance) group_left(release) node_uname_info
```

or

```promql
node_uname_info * on(instance) group_right(release)
(
  (
    time() - node_boot_time_seconds
  ) / 60
)
```
{{% /details %}}
